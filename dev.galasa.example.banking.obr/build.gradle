// This section tells gradle which gradle plugins to use to build this project.
plugins {
    id 'base'
    id 'maven-publish'
    id 'dev.galasa.obr' version '0.32.0'
    id 'dev.galasa.testcatalog' version '0.32.0'
}

// Set the variables which will control what the built OSGi bundle will be called
// and the name it will be published under in the maven repository.
group = 'dev.galasa.example.banking'
version = '0.0.1-SNAPSHOT'

// What are the dependencies of the obr ? 
dependencies {
    bundle project(':dev.galasa.example.banking.payee')
    bundle project(':dev.galasa.example.banking.account')
}

def obrFile = file('build/galasa.obr')
artifacts {
    archives obrFile
}

// Tell gradle to publish the built OBR as a maven artifact on the 
// local maven repository.
publishing {
    publications {
        maven(MavenPublication) {
            artifact obrFile
        }
    }
}

deployTestCatalog {
    if ( System.getProperty("GALASA_BOOTSTRAP") != null) {
        bootstrap = System.getProperty("GALASA_BOOTSTRAP")
    } else {
        bootstrap = "none"
    }

    if (System.getProperty("GALASA_STREAM")!= null) {
        stream = System.getProperty("GALASA_STREAM")
    } else {
        stream = "none"
    }

    if(System.getProperty("GALASA_TOKEN") != null) {
        galasaToken = System.getProperty("GALASA_TOKEN");
    } else {
        galasaToken = "none"
    }
}
